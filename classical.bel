% Classical logic using Peirce's law

i : type.

LF o : type =  % formulas
  | ⊃ : o → o → o
  | ⊤ : o
  | ⊥ : o
  | ∧   : o → o → o
  | ∨   : o → o → o
;


--infix ∧ 6 right.
--infix ∨ 5 right.
--infix ⊃ 4 right.


% Classical Natural Deduction Rules
LF cnd : o → type = 
  % Natural Deduction
  | ⊃I : (cnd A → cnd B)
	 → cnd (A ⊃ B)
  | ⊃E : cnd (A ⊃ B) → cnd A
	 → cnd B
  | ∧I : cnd A → cnd B → cnd (A ∧ B)
  | ∧El : cnd (A ∧ B) → cnd A
  | ∧Er : cnd (A ∧ B) → cnd B

  | ∨Il : cnd A → cnd (A ∨ B)
  | ∨Ir : cnd B → cnd (A ∨ B)
  | ∨E  : cnd (A ∨ B)
        → (cnd A → cnd C)
        → (cnd B → cnd C)        
        → cnd C

  | ⊤I : cnd ⊤
  | ⊥E : cnd ⊥ → cnd C

  % Peirce's Law
  | PL: (cnd (A ⊃ ⊥) → cnd A) → cnd A

;



% Proof of Law of excluded middle
rec exm : [ ⊢ cnd (A ∨ (A ⊃ ⊥))] =
[ ⊢ PL (\u. ∨Ir (
          ⊃I (\v.
            ⊃E u (∨Il v)
          )
))
  ] 
;





% Proof Terms
LF tm : type =
  | app : tm → tm → tm
  | lam : (tm → tm) → tm
  
  | pair : tm → tm → tm
  | fst : tm → tm
  | snd : tm → tm

  | inl : tm → tm
  | inr : tm → tm
  | cse : tm → (tm → tm) → (tm → tm) → tm

  | taut : tm
  | abort : tm → tm 

  | throw : stack → tm → tm 

  | letcc : (stack → tm) → tm


% Stack 
and stack : type =
  | s_lam : tm → stack → stack % app _ N k
  | s_app : tm → stack → stack % app M _
  
  | s_pair_l : tm → stack → stack
  | s_pair_r : tm → stack → stack
  | s_fst : stack → stack
  | s_snd : stack → stack

  | s_inl : stack → stack
  | s_inr : stack → stack
  | s_cse : (tm → tm) → (tm → tm) → stack → stack

  | s_abort : stack → stack

  | halt : stack

;


% Use proof terms
LF pt_true : tm → o → type = 
  % Implication Intro x:A, M:B → lam x. M : A⊃B
  % Here we have (M x):B. It is saying M is the function body,and x is "applied" to it
  % so to extract the function body, lam M : A⊃B
  | ⊃I : ({x : tm} pt_true x A → pt_true (M x) B)  → pt_true (lam (\x. M x)) (A ⊃ B)

  % Implication Elim M:A⊃B, N:A → M N: B
  | ⊃E : pt_true M (A ⊃ B) → pt_true N A → pt_true (app M N) B

  | ∧I : pt_true M A → pt_true N B → pt_true (pair M N) (A ∧ B)
  | ∧El : pt_true M (A ∧ B) → pt_true (fst M) A
  | ∧Er : pt_true M (A ∧ B) → pt_true (snd M) B

  | ∨Il : pt_true M A → pt_true (inl M) (A ∨ B)
  | ∨Ir : pt_true M B → pt_true (inr M) (A ∨ B)
  | ∨E  : pt_true M (A ∨ B)
        → ({x : tm} pt_true x A → pt_true (Nl x) C)
        → ({y : tm} pt_true y B → pt_true (Nr y) C)   
        → pt_true (cse M (\x. Nl x) (\y. Nr y)) C

  | ⊤I : pt_true taut ⊤

  | ⊥E : pt_true M ⊥ → pt_true (abort M) C

  | throw_app : pt_false k A → pt_true M A → pt_true (throw k M) ⊥

  | PL: ({a : stack} pt_false a A → pt_true M A) → pt_true (letcc (\a. M)) A


and pt_false: stack → o → type = 
  | Ax : pt_false halt M
  | ⊥FI : pt_false k M → pt_false (s_abort k) ⊥

  | ⊃FI : pt_true N A → pt_false k B → pt_false (s_lam N k) (A ⊃ B)
  | ⊃FE : pt_true M (A ⊃ B) → pt_false k B → pt_false (s_app M k) A

  | ∧FEl : pt_true N B → pt_false k (A ∧ B) → pt_false (s_pair_l N k) A
  | ∧FEr : pt_true M A → pt_false k (A ∧ B) → pt_false (s_pair_r M k) B

  | ∧FIl : pt_false k A → pt_false (s_fst k) (A ∧ B)
  | ∧FIr : pt_false k B → pt_false (s_snd k) (A ∧ B)

  | ∨FEl : pt_false k (A ∨ B) → pt_false (s_inl k) A
  | ∨FEr : pt_false k (A ∨ B) → pt_false (s_inr k) B

  | ∨FI  :({x : tm} pt_true x A → pt_true (M x) C)
        → ({y : tm} pt_true y B → pt_true (N y) C)   
        → pt_false k C
        → pt_false (s_cse (\x. M x) (\y. N y) k) (A ∨ B)
;






LF joint : type =
  | config: tm → stack → joint
;


% With both contexts
LF pt_conf : joint → o → type = 
  | joined : pt_true M A → pt_false k A → pt_conf (config M k) A
;


% Values 
LF value : tm → type =
  | value_taut : value taut
  | value_lam : value (lam (\x. M x))
  | value_pair : value M → value N → value (pair M N)
  | value_inl : value M → value (inl M)
  | value_inr : value M → value (inr M)
  ;


% Terminal
LF terminal : joint → type =
  | term_taut : terminal (config taut halt)
  | term_lam : terminal (config (lam (\x. M x)) halt)
  | term_pair : value M → value N → terminal (config (pair M N) halt)
  | term_inl : value M → terminal (config (inl M) halt)
  | term_inr : value M → terminal (config (inr M) halt)
;


% Small step
LF step : joint → joint → type = 
  | step_abort : (step (config (abort M) k) (config M (s_abort k)))

  | step_throw : (step (config (throw k' M') k) (config M' k'))

  | step_lam : (step (config (app M N) k) (config M (s_lam N k)))

  | step_app : value (lam (\x. M x)) → (step (config (lam (\x. M' x)) (s_lam N k)) (config N (s_app (lam (\x. M' x)) k)))

  | step_app_lam : value (lam (\x. M x)) → value v → (step (config v (s_app (lam (\x. (M' x))) k)) (config (M' v) k))

  | step_pair_l : (step (config (pair M N) k) (config M (s_pair_l N k)))

  | step_pair_r : value v → (step (config v (s_pair_l N k)) (config N (s_pair_r v k)))

  | step_pair_comp : value v → value w → (step (config w (s_pair_r v k)) (config (pair v w) k))

  | step_fst : (step (config (fst M) k) (config M (s_fst k)))
  | step_snd : (step (config (snd M) k) (config M (s_snd k)))

  | step_fst_pair : value v → value w → (step (config (pair v w) (s_fst k)) (config v k))
  | step_snd_pair : value v → value w → (step (config (pair v w) (s_snd k)) (config w k))

  | step_inl : (step (config (inl M) k) (config M (s_inl k)))
  | step_inr : (step (config (inr M) k) (config M (s_inr k)))

  | step_inl_comp : value v → (step (config v (s_inl k)) (config (inl v) k))
  | step_inr_comp : value v → (step (config v (s_inr k)) (config (inr v) k))

  | step_cse : (step (config (cse L (\x. M x) (\y. N y)) k) (config L (s_cse (\x. M x) (\y. N y) k)))

  | step_cse_inl : value v → (step (config (inl v) (s_cse (\x. (M x)) (\y. (N y)) k)) (config (M v) k))
  | step_cse_inr : value v → (step (config (inr v) (s_cse (\x. (M x)) (\y. (N y)) k)) (config (N v) k))
  
  | step_let_cc : (step (config (letcc (\a. (M a))) k) (config (M k) k))

;




LF tm_sub: tm → tm → type =
  | sub_app_lam : value N → tm_sub (app (lam (\x. M x)) N) (M N)
  
  | sub_fst_pair : tm_sub (fst (pair M N)) M
  | sub_snd_pair : tm_sub (snd (pair M N)) N
  
  | sub_inl_cse : tm_sub (cse (inl M) (\x. Nl x) (\y. Nr y)) (Nl M)
  | sub_inr_cse : tm_sub (cse (inr M) (\x. Nl x) (\y. Nr y)) (Nr M)

;


rec sub_lemma : [ ⊢ pt_true M P] → [ ⊢ tm_sub M M'] → [ ⊢ pt_true M' P] = 
/ total s (sub_lemma M P M' p s)/
fn p ⇒ fn s ⇒ case s of
  | [ ⊢ sub_app_lam V1] ⇒	
    let [ ⊢ ⊃E (⊃I (\v. \u. D1)) D2] = p in
      [ ⊢ D1[_,D2]]

  | [ ⊢ sub_fst_pair] ⇒	
    let [ ⊢ ∧El (∧I D1 D2)] = p in
      [ ⊢ D1]

  | [ ⊢ sub_snd_pair] ⇒	
    let [ ⊢ ∧Er (∧I D1 D2)] = p in
      [ ⊢ D2]

  | [ ⊢ sub_inl_cse] ⇒	
    let [ ⊢ ∨E (∨Il D1) (\u. \v. D2) (\u. \v. D3)] = p in
      [ ⊢ D2[_,D1]]

  | [ ⊢ sub_inr_cse] ⇒	
    let [ ⊢ ∨E (∨Ir D1) (\u. \v. D2) (\u. \v. D3)] = p in
      [ ⊢ D3[_,D1]]
;



inductive Result : {M : [ ⊢ tm]}{k : [ ⊢ stack]} → ctype =
| Res : {P : [ ⊢ o]} 
      → [ ⊢ pt_conf (config M k) P]
      → Result [ ⊢ M] [ ⊢ k]
;




% Preservation
rec pres :  [ ⊢ pt_conf (config M k) P] → [ ⊢ step (config M k) (config M' k') ] → Result [ ⊢ M'] [ ⊢ k'] = 
/ total s (pres M k P M' k' p s)/
fn p ⇒ fn s ⇒ case s of
  | [ ⊢ step_abort] ⇒	
    let [ ⊢ joined (⊥E D1) (D2)] = p in 
      Res [ ⊢ _] [ ⊢ joined D1 (⊥FI D2)]
      
  | [ ⊢ step_lam] ⇒	
    let [ ⊢ joined (⊃E D1 D2) (D3)] = p in 
      Res [ ⊢ _] [ ⊢ joined D1 (⊃FI D2 D3)]
      
  | [ ⊢ step_app V1] ⇒	
    let [ ⊢ joined D1 (⊃FI D2 D3)] = p in 
      Res [ ⊢ _] [ ⊢ joined D2 (⊃FE D1 D3)]
      
  | [ ⊢ step_app_lam V1 V2] ⇒	
    let [ ⊢ joined D1 (⊃FE D2 D3)] = p in 
    let [ ⊢ D4] = (sub_lemma [ ⊢ ⊃E D2 D1] [ ⊢ sub_app_lam V2]) in
      Res [ ⊢ _] [ ⊢ joined D4 D3]
      
  | [ ⊢ step_pair_l] ⇒	
    let [ ⊢ joined (∧I D1 D2) (D3)] = p in 
      Res [ ⊢ _] [ ⊢ joined D1 (∧FEl D2 D3)]
      
  | [ ⊢ step_pair_r V1] ⇒	
    let [ ⊢ joined D1 (∧FEl D2 D3)] = p in 
      Res [ ⊢ _] [ ⊢ joined D2 (∧FEr D1 D3)]
      
  | [ ⊢ step_pair_comp V1 V2] ⇒	
    let [ ⊢ joined D1 (∧FEr D2 D3)] = p in 
      Res [ ⊢ _] [ ⊢ joined (∧I D2 D1) D3]
      
  | [ ⊢ step_fst] ⇒	
    let [ ⊢ joined (∧El D1) (D2)] = p in 
      Res [ ⊢ _] [ ⊢ joined D1 (∧FIl D2)]
      
  | [ ⊢ step_snd] ⇒	
    let [ ⊢ joined (∧Er D1) (D2)] = p in 
      Res [ ⊢ _] [ ⊢ joined D1 (∧FIr D2)]
      
  | [ ⊢ step_fst_pair V1 V2] ⇒	
    let [ ⊢ joined (∧I D1 D2) (∧FIl D3)] = p in 
      Res [ ⊢ _] [ ⊢ joined D1 D3]
      
  | [ ⊢ step_snd_pair V1 V2] ⇒	
    let [ ⊢ joined (∧I D1 D2) (∧FIr D3)] = p in 
      Res [ ⊢ _] [ ⊢ joined D2 D3]
      
  | [ ⊢ step_inl] ⇒	
    let [ ⊢ joined (∨Il D1) (D2)] = p in 
      Res [ ⊢ _] [ ⊢ joined D1 (∨FEl D2)]
      
  | [ ⊢ step_inr] ⇒	
    let [ ⊢ joined (∨Ir D1) (D2)] = p in 
      Res [ ⊢ _] [ ⊢ joined D1 (∨FEr D2)]
      
  | [ ⊢ step_inl_comp V] ⇒	
    let [ ⊢ joined D1 (∨FEl D2)] = p in 
      Res [ ⊢ _] [ ⊢ joined (∨Il D1) D2]
      
  | [ ⊢ step_inr_comp V] ⇒	
    let [ ⊢ joined D1 (∨FEr D2)] = p in 
      Res [ ⊢ _] [ ⊢ joined (∨Ir D1) D2]
      
  | [ ⊢ step_cse] ⇒	
    let [ ⊢ joined (∨E D1 (\x. \d. D2) (\y. \d. D3)) D4] = p in 
      Res [ ⊢ _] [ ⊢ joined D1 (∨FI (\x. \d. D2) (\y. \d. D3) D4)]
      
  | [ ⊢ step_cse_inl V] ⇒	
    let [ ⊢ joined D1 (∨FI (\x. \d. D2) (\y. \d. D3) D4)] = p in 
    let [ ⊢ D5] = (sub_lemma [ ⊢ (∨E D1 (\x. \d. D2) (\y. \d. D3))] [ ⊢ sub_inl_cse]) in
      Res [ ⊢ _] [ ⊢ joined D5 D4]
      
  | [ ⊢ step_cse_inr V] ⇒	
    let [ ⊢ joined D1 (∨FI (\x. \d. D2) (\y. \d. D3) D4)] = p in 
    let [ ⊢ D5] = (sub_lemma [ ⊢ (∨E D1 (\x. \d. D2) (\y. \d. D3))] [ ⊢ sub_inr_cse]) in
      Res [ ⊢ _] [ ⊢ joined D5 D4]
      
  | [ ⊢ step_throw] ⇒
    let [ ⊢ joined (throw_app D1 D2) D3] = p in 
      Res [ ⊢ _] [ ⊢ joined D2 D1]
      
  | [ ⊢ step_let_cc] ⇒
    let [ ⊢ joined (PL (\a. \d. D1)) D2] = p in 
      Res [ ⊢ _] [ ⊢ joined D1[_,D2] D2]
;



LF step_true : tm → tm → type = 
  | st_app_l : (step_true A1 A2) → (step_true (app A1 B) (app A2 B))
  | st_app_r : (value A) → (step_true B1 B2) → (step_true (app A B1) (app A B2))

  | st_pair_l : (step_true A1 A2) → (step_true (pair A1 B) (pair A2 B))
  | st_pair_r : (value A) → (step_true B1 B2) → (step_true (pair A B1) (pair A B2))
  
  | st_inl : (step_true A1 A2) → (step_true (inl A1) (inl A2))
  | st_inr : (step_true A1 A2) → (step_true (inr A1) (inr A2))
;


% Step or value
LF step_or_val : tm -> type =
  | st_to : (step_true E E') -> step_or_val E
  | vals : (value E) -> step_or_val E
;



% Progress (if we have proof then the proof term is a step or a value)
rec prog_tm : [ ⊢ pt_true E P] → [ ⊢ step_or_val E] = 
fn p ⇒ case p of
  | [ ⊢ ⊤I] ⇒ [ ⊢ vals value_taut]

  | [ ⊢ ⊃I (\x. \d. D)] ⇒  [ ⊢ vals value_lam]

  | [ ⊢ ∧I D1 D2] ⇒	
    (case prog_tm [ ⊢ D1]  of
      | [ ⊢ st_to EV] ⇒ [ ⊢ st_to (st_pair_l EV)]
      | [ ⊢ vals VL1] ⇒ 
        (case  prog_tm [ |- D2]   of
          | [ |- st_to EV] => [ |- st_to (st_pair_r VL1 EV)]
          | [ |- vals VL2] => [ |- vals (value_pair VL1 VL2)]
       )
    )

  | [ ⊢ ∨Il D] ⇒		
    (case prog_tm [ ⊢ D]  of
      | [ ⊢ st_to EV] ⇒ [ ⊢ st_to (st_inl EV)]
      | [ ⊢ vals V] ⇒ [ ⊢ vals (value_inl V)] 
    )
  
  | [ ⊢ ∨Ir D] ⇒		
    (case prog_tm [ ⊢ D]  of
      | [ ⊢ st_to EV] ⇒ [ ⊢ st_to (st_inr EV)]
      | [ ⊢ vals V] ⇒ [ ⊢ vals (value_inr V)] 
    )
;



LF step_or_terminate: joint → type =
 | step_to: (step C C') → step_or_terminate C
 | terminate: terminal C → step_or_terminate C
;






% Progress 
rec prog : [ ⊢ pt_conf C P] → [ ⊢ step_or_terminate C] = 
fn p ⇒ case p of
    | [ ⊢ joined T1 F1] ⇒ 
      (case [ ⊢ T1]  of
        | [ ⊢ ⊤I] ⇒ 
          (case [ ⊢ F1]  of
              | [ ⊢ Ax] ⇒ [ ⊢ terminate term_taut]
          )
        | [ ⊢ ⊥E D1] ⇒ 
          (case [ ⊢ F1]  of
              | [ ⊢ ⊥FI E1] ⇒ 
                (case prog [ ⊢ joined D1 F1] of
                  | [ |- step_to EV] => [ |- step_to (step_abort)]
                )
          )
          
        | [ ⊢ ⊃I (\x. \d. D1)] ⇒ 
          (case [ ⊢ F1]  of
              | [ ⊢ Ax] ⇒ [ ⊢ terminate term_lam]
              | [ ⊢ ⊃FI D2 E1] ⇒ 
                (case prog_tm [ ⊢ T1] of
                    | [ |- vals V] =>  [ |- step_to (step_app V)]
                )
                
              | [ ⊢ ⊃FE D2 E1] ⇒ 
                (case prog_tm [ ⊢ T1] of
                  | [ ⊢ vals V1] => 
                    (case [ ⊢ D2] of
                      | [ ⊢ ⊃I (\x. \d. D3)] =>
                        (case prog_tm [ ⊢ D2] of
                          | [ ⊢ vals V2] => [ |- step_to (step_app_lam V2 V1)]
                        )
                    )
                )

              | [ ⊢ ∧FEl D2 E1] ⇒  
                (case prog_tm [ ⊢ T1] of
                    | [ |- vals V] =>  [ |- step_to (step_pair_r V)]
                )

              | [ ⊢ ∧FEr D2 E1] ⇒  
                (case prog_tm [ ⊢ T1] of
                  | [ ⊢ vals V1] => 
                    (case [ ⊢ D2] of
                      | [ ⊢ ∧I D3 D4] =>
                        (case prog_tm [ ⊢ D2] of
                          | [ ⊢ vals V2] => [ |- step_to (step_pair_comp V2 V1)]
                        )
                    )
                )

              | [ ⊢ ∨FEl E1] ⇒  
                (case prog_tm [ ⊢ T1] of
                    | [ |- vals V] =>  [ |- step_to (step_inl_comp V)]
                )

              | [ ⊢ ∨FEr E1] ⇒  
                (case prog_tm [ ⊢ T1] of
                    | [ |- vals V] =>  [ |- step_to (step_inr_comp V)]
                )
          )
        | [ ⊢ ⊃E D1 D2] => [ |- step_to (step_lam)]

        | [ ⊢ ∧I D1 D2] ⇒ 
          (case prog_tm [ ⊢ D1] of
            | [ |- st_to S1] ⇒ [ |- step_to (step_pair_l)]
            | [ |- vals V1] ⇒
              (case prog_tm [ ⊢ D2] of
                | [ |- vals V2] ⇒
                  (case [ ⊢ F1]  of
                    | [ ⊢ Ax] ⇒ [ |- terminate (term_pair V1 V2)]
                    | [ ⊢ ⊃FE D3 E1] ⇒  
                      (case [ ⊢ D3] of
                        | [ ⊢ ⊃I (\x. \d. D4)] =>
                          (case prog_tm [ ⊢ D3] of
                            | [ ⊢ vals V4] => [ |- step_to (step_app_lam V4 (value_pair V1 V2))]
                          )
                      )
                    
                    | [ ⊢ ∧FEl D3 E1] ⇒  [ |- step_to (step_pair_r (value_pair V1 V2))]

                    | [ ⊢ ∧FEr D3 E1] ⇒  
                      (case [ ⊢ D3] of
                        | [ ⊢ ∧I D4 D5] =>
                          (case prog_tm [ ⊢ D3] of
                            | [ ⊢ vals V3] => [ |- step_to (step_pair_comp V3 (value_pair V1 V2))]
                          )
                      ) 

                    | [ ⊢ ∨FEl E1] ⇒ [ |- step_to (step_inl_comp (value_pair V1 V2))]
                      

                    | [ ⊢ ∨FEr E1] ⇒ [ |- step_to (step_inr_comp (value_pair V1 V2))]
                      
                    | [ ⊢ ∧FIl E1] ⇒ [ |- step_to (step_fst_pair V1 V2)]

                    | [ ⊢ ∧FIr E1] ⇒ [ |- step_to (step_snd_pair V1 V2)]
                  )
              )
          )

        

        | [ ⊢ ∧El D1] ⇒ [ |- step_to (step_fst)]
        | [ ⊢ ∧Er D1] ⇒ [ |- step_to (step_snd)]

        | [ ⊢ ∨Il D1] ⇒ 
          (case prog_tm [ ⊢ D1] of
            | [ |- st_to S1] ⇒ [ |- step_to (step_inl)]
            | [ |- vals V1] ⇒
              (case [ ⊢ F1]  of
                | [ ⊢ Ax] ⇒ [ |- terminate (term_inl V1)]
                | [ ⊢ ⊃FE D2 E1] ⇒  
                  (case [ ⊢ D2] of
                    | [ ⊢ ⊃I (\x. \d. D3)] =>
                      (case prog_tm [ ⊢ D2] of
                        | [ ⊢ vals V2] => [ |- step_to (step_app_lam V2 (value_inl V1))]
                      )
                  )
                
                | [ ⊢ ∧FEl D2 E1] ⇒  [ |- step_to (step_pair_r (value_inl V1))]

                | [ ⊢ ∧FEr D2 E1] ⇒  
                  (case [ ⊢ D2] of
                    | [ ⊢ ∧I D3 D4] =>
                      (case prog_tm [ ⊢ D2] of
                        | [ ⊢ vals V2] => [ |- step_to (step_pair_comp V2 (value_inl V1))]
                      )
                  ) 

                | [ ⊢ ∨FEl E1] ⇒ [ |- step_to (step_inl_comp (value_inl V1))]
                  

                | [ ⊢ ∨FEr E1] ⇒ [ |- step_to (step_inr_comp (value_inl V1))]

                | [ ⊢ ∨FI (\x. \d. D3) (\x. \d. D4) E1] ⇒ [ |- step_to (step_cse_inl V1)]
              )
          )




        | [ ⊢ ∨Ir D1] ⇒ 
          (case prog_tm [ ⊢ D1] of
            | [ |- st_to S1] ⇒ [ |- step_to (step_inr)]
            | [ |- vals V1] ⇒
              (case [ ⊢ F1]  of
                | [ ⊢ Ax] ⇒ [ |- terminate (term_inr V1)]
                | [ ⊢ ⊃FE D2 E1] ⇒  
                  (case [ ⊢ D2] of
                    | [ ⊢ ⊃I (\x. \d. D3)] =>
                      (case prog_tm [ ⊢ D2] of
                        | [ ⊢ vals V2] => [ |- step_to (step_app_lam V2 (value_inr V1))]
                      )
                  )
                
                | [ ⊢ ∧FEl D2 E1] ⇒  [ |- step_to (step_pair_r (value_inr V1))]

                | [ ⊢ ∧FEr D2 E1] ⇒  
                  (case [ ⊢ D2] of
                    | [ ⊢ ∧I D3 D4] =>
                      (case prog_tm [ ⊢ D2] of
                        | [ ⊢ vals V2] => [ |- step_to (step_pair_comp V2 (value_inr V1))]
                      )
                  ) 

                | [ ⊢ ∨FEl E1] ⇒ [ |- step_to (step_inl_comp (value_inr V1))]
                  

                | [ ⊢ ∨FEr E1] ⇒ [ |- step_to (step_inr_comp (value_inr V1))]

                | [ ⊢ ∨FI (\x. \d. D3) (\x. \d. D4) E1] ⇒ [ |- step_to (step_cse_inr V1)]
              )
          )
        
        | [ ⊢ ∨E D1 (\x. \d. D2) (\y. \e. D3)] ⇒ [ |- step_to (step_cse)]


        | [ ⊢ throw_app D1 D2] ⇒ [ |- step_to (step_throw)]
        | [ ⊢ PL (\a. \d. D1)] ⇒ [ |- step_to (step_let_cc)]
      )
;


